/******************************************************************************/
/*                                                                            */
/*  @SRCLIB  - библиотека, где расположены файлы исходного кода;              */
/*  @DSTLIB  - библиотека, куда будут помещены собранные объекты.             */
/*                                                                            */
/******************************************************************************/

PGM PARM(&@SRCLIB &@DSTLIB)

  /* ВХОДНЫЕ ПАРАМЕТРЫ ********************************************************/
  DCL        VAR(&@SRCLIB)    TYPE(*CHAR) LEN(10)
  DCL        VAR(&@DSTLIB)    TYPE(*CHAR) LEN(10)



  /* ВНУТРЕННИЕ ПЕРЕМЕННЫЕ ****************************************************/
  DCL        VAR(&TAG)        TYPE(*LGL)           VALUE('0')
  DCL        VAR(&SRCLIB)     TYPE(*CHAR) LEN(10)  VALUE(CSNSRC)                /* Библиотека с файлом исходного кода */
  DCL        VAR(&DSTLIB)     TYPE(*CHAR) LEN(10)  VALUE(OLI)                   /* Библиотека для размещения собранных объектов */
  DCL        VAR(&TMPLIB)     TYPE(*CHAR) LEN(10)  VALUE(QTEMP)                 /* Временная библиотека */
  DCL        VAR(&DBGMOD)     TYPE(*CHAR) LEN(10)  VALUE(*NONE)                 /* Режим отладки */
  DCL        VAR(&OPTMOD)     TYPE(*INT)  LEN(2)                                /* Уровень оптимизации */
  DCL        VAR(&LOWKEY)     TYPE(*CHAR) LEN(4)
  DCL        VAR(&MSGKEY)     TYPE(*CHAR) LEN(4)
  DCL        VAR(&HIKEY)      TYPE(*CHAR) LEN(4)
  DCL        VAR(&MSG)        TYPE(*CHAR) LEN(132)
  DCL        VAR(&MSGID)      TYPE(*CHAR) LEN(7)
  DCL        VAR(&I)          TYPE(*INT)  LEN(2)
  DCL        VAR(&SEV)        TYPE(*DEC)  LEN(2)
  DCL        VAR(&RTNTYPE)    TYPE(*CHAR) LEN(2)
  DCL        VAR(&SIGN)       TYPE(*CHAR) LEN(1)

  CALLSUBR   INITIALIZE                                                         /* Инициализация переменных */




  /* ОЧИСТКА ******************************************************************/
  DLTSRVPGM  SRVPGM(&DSTLIB/GTEST)
  MONMSG     MSGID(CPF0000)




  /* КОМПИЛЯЦИЯ МОДУЛЕЙ *******************************************************/
  CHGLIBL    LIBL(&SRCLIB)

  CRTCPPMOD  MODULE(&TMPLIB/GTEST) SRCFILE(&SRCLIB/GTEST) SRCMBR(GTEST$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/DEATH_TST) SRCFILE(&SRCLIB/GTEST) SRCMBR(DEATH_TST$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/FILEPATH) SRCFILE(&SRCLIB/GTEST) SRCMBR(FILEPATH$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/PORT) SRCFILE(&SRCLIB/GTEST) SRCMBR(PORT$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/PRINTERS) SRCFILE(&SRCLIB/GTEST) SRCMBR(PRINTERS$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/TEST_PART) SRCFILE(&SRCLIB/GTEST) SRCMBR(TEST_PART$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)

  CRTCPPMOD  MODULE(&TMPLIB/TYPED_TST) SRCFILE(&SRCLIB/GTEST) SRCMBR(TYPED_TST$) +
             DBGVIEW(&DBGMOD) OPTIMIZE(&OPTMOD) +
             OPTION(*EVENTF) PFROPT(*STRDONLY) LANGLVL(*EXTENDED0X) +
             CHECKOUT(*COND *PARM *REACH *UNUSED)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADMOD)




  /* СБОРКА ПРОГРАММ **********************************************************/
  CHGLIBL    LIBL(&TMPLIB)
  SNDPGMMSG  MSG('BEGIN MONITORING') KEYVAR(&LOWKEY)

  CRTSRVPGM  SRVPGM(&DSTLIB/GTEST) EXPORT(*ALL) +
             MODULE(GTEST DEATH_TST FILEPATH PORT PRINTERS TEST_PART TYPED_TST)
  MONMSG     MSGID(CPF0000) EXEC(GOTO BADBUILD)




  /* ТОЧКИ ВЫХОДА *************************************************************/
  GOOD:                                                                         /* Все объекты собраны успешно */
  SNDPGMMSG  MSG('BEGIN MONITORING') KEYVAR(&LOWKEY)
  SNDPGMMSG  MSGID(CPA2401) MSGF(QCPFMSG) MSGTYPE(*INFO) +
             MSGDTA('Done successful!')
  GOTO       FINALLY

  BADMOD:                                                                       /* Не удалось выполнить компиляцию модуля */
  SNDPGMMSG  MSG('BEGIN MONITORING') KEYVAR(&LOWKEY)
  SNDPGMMSG  MSGID(CPA2401) MSGF(QCPFMSG) MSGTYPE(*DIAG) +
             MSGDTA('Module compilation failed...')
  GOTO       FINALLY

  BADBUILD:                                                                     /* Не удалось выполнить сборку программы */
  SNDPGMMSG  MSGID(CPA2401) MSGF(QCPFMSG) MSGTYPE(*DIAG) +
             MSGDTA('Program build failed...')
  GOTO       FINALLY




  /* ИЗВЛЕЧЕНИЕ СООБЩЕНИЙ ИЗ ЛОГА ЗАДАНИЯ В ФАЙЛ ОШИБОК ***********************/
  FINALLY:
  SNDPGMMSG  MSG('END MONITORING') KEYVAR(&HIKEY)

  RMVMSG     MSGKEY(&LOWKEY)
  RMVMSG     MSGKEY(&HIKEY)
  CHGVAR     VAR(%BIN(&MSGKEY 1 4)) VALUE(%BIN(&LOWKEY 1 4) + 1)

  DLTF       FILE(&TMPLIB/LOGMSG)
  MONMSG     MSGID(CPF0000)
  CRTPF      FILE(QTEMP/LOGMSG) RCDLEN(300) MAXMBRS(*NOMAX)

  RUNSQL     SQL('insert into QTEMP/LOGMSG values +
                  (''TIMESTAMP  0 20210211140117''), +
                  (''PROCESSOR  0 000 1'')') +
             COMMIT(*NONE)

  LOOP:                                                                         /* Цикл извлечения сообщений */
    RCVMSG PGMQ(*SAME (*)) MSGKEY(&MSGKEY) RMV(*NO) +
           MSG(&MSG) MSGID(&MSGID) SEV(&SEV) RTNTYPE(&RTNTYPE)
    MONMSG CPF2410 EXEC(GOTO CONTINUE)

    DOFOR &I 1 %LEN(&MSG)
      CHGVAR VAR(&I) VALUE(%SCAN('''' &MSG))
      IF (&I = 0) LEAVE
      CHGVAR VAR(%SUBSTRING(&MSG &I 1)) VALUE('"')
    ENDDO

    SELECT
      WHEN COND(&RTNTYPE = '15') THEN(CHGVAR VAR(&SIGN) VALUE('E'))
      WHEN COND(&RTNTYPE = '02') THEN(DO)
        IF (&SEV *LE 10)         THEN(CHGVAR VAR(&SIGN) VALUE('W'))
        ELSE                          CHGVAR VAR(&SIGN) VALUE('S')
      ENDDO
      OTHERWISE                   CMD(CHGVAR VAR(&SIGN) VALUE('I'))
    ENDSELECT

    RUNSQL SQL('insert into QTEMP/LOGMSG values +
                ( +
                  ''ERROR      1 001 0 000000 000000 000 000000 000 ' +
                  *CAT &MSGID *CAT ' ' *CAT &SIGN *CAT ' ' *CAT  +
                  %CHAR(&SEV) *CAT '132 ' *CAT %TRIM(&MSG) *CAT ''' +
                )') +
           COMMIT(*NONE)

    CONTINUE:
    CHGVAR VAR(%BIN(&MSGKEY 1 4)) VALUE(%BIN(&MSGKEY 1 4) + 1)
    IF (&MSGKEY *LT &HIKEY) GOTO LOOP
  ENDLOOP:



  /* ПЕРЕНОС СООБЩЕНИЙ В ФАЙЛ ОШИБОК ******************************************/
  CPYF       FROMFILE(QTEMP/LOGMSG) TOFILE(&SRCLIB/EVFEVENT) +
             FROMMBR(*ALL) TOMBR(*FROMMBR) MBROPT(*ADD) CRTFILE(*YES) +
             FMTOPT(*NOCHK)

  CPYF       FROMFILE(&DSTLIB/EVFEVENT) TOFILE(&SRCLIB/EVFEVENT) +
             FROMMBR(*ALL) TOMBR(*FROMMBR) MBROPT(*ADD) FMTOPT(*NOCHK)
  MONMSG     MSGID(CPF0000)
  DLTF       FILE(&DSTLIB/EVFEVENT)
  MONMSG     MSGID(CPF0000)

  CPYF       FROMFILE(&TMPLIB/EVFEVENT) TOFILE(&SRCLIB/EVFEVENT) +
             FROMMBR(*ALL) TOMBR(*FROMMBR) MBROPT(*ADD) FMTOPT(*NOCHK)
  MONMSG     MSGID(CPF0000)
  DLTF       FILE(&TMPLIB/EVFEVENT)
  MONMSG     MSGID(CPF0000)

  IF (&DBGMOD = *NONE) DO
    DLTF       FILE(&SRCLIB/EVFEVENT)
    MONMSG     MSGID(CPF0000)
  ENDDO



  /* ПОДПРОЦЕДУРА ИНИЦИАЛИЗАЦИИ ************************************************/
  SUBR INITIALIZE

  CHKOBJ     OBJ(QSYS/&@SRCLIB) OBJTYPE(*LIB)                                   /* Обработка входных параметров */
  MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&TAG) VALUE('1'))
  IF (*NOT &TAG) CHGVAR VAR(&SRCLIB) VALUE(&@SRCLIB)
  CHGVAR     VAR(&TAG) VALUE('0')

  CHKOBJ     OBJ(QSYS/&@DSTLIB) OBJTYPE(*LIB)
  MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&TAG) VALUE('1'))
  IF (*NOT &TAG) CHGVAR VAR(&DSTLIB) VALUE(&@DSTLIB)
  CHGVAR     VAR(&TAG) VALUE('0')

  DLTF       FILE(&SRCLIB/EVFEVENT)                                             /* Удаление файла ошибок */
  MONMSG     MSGID(CPF0000 MCH0000 RPG0000)

  DLTF       FILE(&DSTLIB/EVFEVENT)
  MONMSG     MSGID(CPF0000)

  DLTF       FILE(&TMPLIB/EVFEVENT)
  MONMSG     MSGID(CPF0000)

  IF (&DBGMOD = *ALL) DO
    CHGVAR VAR(&OPTMOD) VALUE(10)
    ENDDO
  ELSE DO
    CHGVAR VAR(&OPTMOD) VALUE(40)
  ENDDO

  CHGLIBL    LIBL(*NONE) CURLIB(*CRTDFT)                                        /* Очистка либла */

  ENDSUBR



ENDPGM
